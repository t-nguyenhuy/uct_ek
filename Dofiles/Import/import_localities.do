
* Note: IDentifiers
* Original SHP ID-s:		LOCID (Locality) ; dhis2_id (sub-county) ; SLID (sub-locality)
* ID-s generated by STATA: 	localityID ; subcounty_ID ; sublocality_ID


* ---------------------------------------------------------------------------
* Use Kenya's administrative shapefiles to load localities and sub-localities
clear
	capture mkdir "$uctek_dtInt/geo_info/"
	
	
	* #################
	** Sub-County-level
	shp2dta using "$uctek_dtRaw/adm_boundaries/ke_subcounty/ke_subcounty.shp", 	///
		database("$uctek_dtInt/geo_info/ke_subcounty.dta") 						///
		coordinates("$uctek_dtInt/geo_info/ke_subcounty_coord.dta") replace		///
		genid(subcounty_ID) gencentroids(centroid)
	
	use "$uctek_dtInt/geo_info/ke_subcounty.dta", clear
		
	
	
	* #################
	** Locality Level
	shp2dta using "$uctek_dtRaw/adm_boundaries/kenlocations/Ken_Locations/Ken_Locations.shp", ///
		database("$uctek_dtInt/geo_info/Ken_locations.dta") 					///
		coordinates("$uctek_dtInt/geo_info/Ken_locations_coord.dta") replace	///
		genid(locality_ID) gencentroids(centroid)
		
	
	use "$uctek_dtInt/geo_info/Ken_locations.dta", clear
			
		** Merge with sub-county 
		geoinpoly y_centroid x_centroid using "$uctek_dtInt/geo_info/ke_subcounty_coord.dta", unique
		rename _ID subcounty_ID
		merge m:m subcounty_ID using "$uctek_dtInt/geo_info/ke_subcounty.dta", ///
			keepusing(provpcode province ctypcode county scpcode subcounty dhis2_id) keep(match master)
			rename *_centroid 	*_locality_centroid
		
		** There are some duplicate LOCID: they are neighbours but not overlapping
		duplicates tag LOCID , gen(_dup)
		
		lab var locality_ID				"Locality ID (STATA)"
		lab var subcounty_ID			"Sub-County ID (STATA)"
		drop _dup _merge
		
		save "$uctek_dtInt/geo_info/GEO_sCounty-Locality.dta", replace
		
	
	
	
	** Sub-Locality Level
	shp2dta using "$uctek_dtRaw/adm_boundaries/kensublocations/Ken_Sublocations/Ken_Sublocations.shp", ///
		database("$uctek_dtInt/geo_info/Ken_sublocations.dta") 					///
		coordinates("$uctek_dtInt/geo_info/Ken_sublocations_coord.dta") replace	///
		genid(sublocality_ID) gencentroids(centroid)
		
	use "$uctek_dtInt/geo_info/Ken_sublocations.dta", clear
	
		drop SLID_REAL
		format %18.0g SLID
	
		
		** Merge with locality
		geoinpoly y_centroid x_centroid using "$uctek_dtInt/geo_info/Ken_locations_coord.dta", unique
		rename _ID locality_ID
		merge m:m locality_ID using "$uctek_dtInt/geo_info/GEO_sCounty-Locality.dta", ///
			keep(master match) gen(_locality_match)
		
		** For those missing, merge with sub-county
		preserve
			keep if _locality_match==1
			
			geoinpoly y_centroid x_centroid if _locality_match!=3 using "$uctek_dtInt/geo_info/ke_subcounty_coord.dta", unique
			
			replace subcounty_ID = _ID 	if	subcounty_ID==. & _ID!=.
			drop provpcode province ctypcode county scpcode subcounty dhis2_id
			
			merge m:m subcounty_ID using  "$uctek_dtInt/geo_info/ke_subcounty.dta", ///
				keepusing(provpcode province ctypcode county scpcode subcounty dhis2_id) ///
				keep(master match) gen(_subcounty_match) 
				drop _ID
			
			tempfile missing_sublocalities
			save	`missing_sublocalities'
		restore
		
		drop if _locality_match==1
		append using `missing_sublocalities'
			
			
		** RCT participation indicators
		local treat_highsat = "609040403,609040401,609040301,609040302,609040303,609040507,609040506," ///
							+ "609030103,609040204,609040201,609030502,609030503,609040103,609030304," ///
							+ "609030303,609030202,609030203,609030404,609030406,609030403,609030401," ///
							+ "609010202,609010101,609010104,609050302,609050305,609050105,609050102," ///
							+ "609050203,609050202,609010301,609020204,609020205,609020203,609020201," ///
							+ "609020303,609020101,609020102,609020103"
		gen rct_area 		= inlist(dhis2_id, "InECuIlzJGx", "Cu46otDi1RO", "m9nsQ1MXlVU")
		gen treat_slocal 	= inlist(SLID, `treat_highsat')	if rct_area==1
		
		
		** Labeling	and formatting
		lab var _locality_match 		"_merge variable for merging with localities"
		lab var _subcounty_match		"_merge variable for merging with subcounties"
		lab var rct_area				"Dummy: Sub-county in RCT"
		lab var treat_slocal			"Dummy: SubLocality is high in high intensity treatment"
		lab def treatslocal 1 "High intensity" 0 "Low intensity"
		lab val treat_slocal treatslocal
	
		save "$uctek_dtInt/geo_info/GEO_sublocality-sCounty-Locality.dta", replace
		
		
		
		
	** Export GEO ID-s for working in QGIS
	use "$uctek_dtInt/geo_info/GEO_sublocality-sCounty-Locality.dta", clear
		keep SLID LOCID dhis2_id
		export delimited using "$uctek_dtInt/geo_info/GEO_sloc_loc_scounty-IDs.csv", delimiter(",") quote replace
		